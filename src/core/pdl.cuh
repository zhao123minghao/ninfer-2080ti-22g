#pragma once

#include <cuda_runtime.h>

#include <cstddef>
#include <utility>

namespace ninfer::pdl {

struct LaunchConfig {
    dim3 grid;
    dim3 block;
    std::size_t dynamic_smem_bytes = 0;
    cudaStream_t stream            = nullptr;
};

// Launches a consumer kernel as a programmatic dependent of the immediately preceding producer
// kernel in the same stream. Every consumer control path that reads producer output must first call
// wait_for_dependencies().
template <class... KernelArgs, class... CallArgs>
[[nodiscard]] inline cudaError_t
launch_dependent(const LaunchConfig& launch, void (*kernel)(KernelArgs...), CallArgs&&... args) {
#if defined(NINFER_SM8X_COMPAT) || defined(NINFER_VOLTA_BUILD)
    kernel<<<launch.grid, launch.block, launch.dynamic_smem_bytes, launch.stream>>>(
        std::forward<CallArgs>(args)...);
    return cudaGetLastError();
#else
    cudaLaunchAttribute attribute{};
    attribute.id = cudaLaunchAttributeProgrammaticStreamSerialization;
    attribute.val.programmaticStreamSerializationAllowed = 1;

    cudaLaunchConfig_t config{};
    config.gridDim          = launch.grid;
    config.blockDim         = launch.block;
    config.dynamicSmemBytes = launch.dynamic_smem_bytes;
    config.stream           = launch.stream;
    config.attrs            = &attribute;
    config.numAttrs         = 1;

    return cudaLaunchKernelEx(&config, kernel, std::forward<CallArgs>(args)...);
#endif
}

// Every producer CTA must call this at least once or exit. This enables dependent scheduling but
// does not make producer writes visible to the consumer.
//
// PDL is a Hopper+ (sm_90+) scheduling hint. On sm_70 (Volta port) there is no grid-dependency
// mechanism to trigger or wait on: the launch attribute in launch_dependent() above is simply not
// honored by the driver for pre-Hopper targets, so producer/consumer ordering there falls back to
// ordinary stream sequencing. These two calls become no-ops rather than relying on the intrinsics
// being defined (and inert) for every arch, which is unverified for the sm_70/CUDA 12.8 toolchain.
__device__ __forceinline__ void trigger_dependents() {
#if __CUDA_ARCH__ >= 900
    cudaTriggerProgrammaticLaunchCompletion();
#endif
}

// Call on every consumer control path before its first access to producer-dependent data.
__device__ __forceinline__ void wait_for_dependencies() {
#if __CUDA_ARCH__ >= 900
    cudaGridDependencySynchronize();
#endif
}

} // namespace ninfer::pdl
